PREFIX db: <http://localhost/rdf/ontology/>
PREFIX dbo: <http://um-cds/ontologies/databaseontology/>
PREFIX roo: <http://www.cancerdata.org/roo/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

DELETE { ?tablerow dbo:has_column ?patientID, ?gender, ?age, ?hpv, ?asa, ?whostatus, ?tstage, ?nstage, ?mstage, ?ajcc, ?tumour, ?metastasis, ?localrecurrence, ?localrecurrencedays, ?regionalrecurrence, ?regionalrecurrencedays, ?metastasisdays, ?rttotaldays, ?graytotaldose, ?graydoseperfraction, ?surgery, ?survival, ?overallsurvivaldays, ?chemo. }

INSERT 
{
	 ?tablerow roo:P100061 ?patientID.   #has_identifier
        
     ?tablerow roo:P100018 ?gender.		 #has_biological_sex
        
     ?tablerow roo:hasage ?age. 
    
     ?age roo:P100027 db:triplifier_test_HN_Maastricht.yearClass.	
        
     ?tablerow roo:P100022 ?hpv.		 #has_finding
        
     ?tablerow roo:P100214 ?asa.		 #has_measurement
        
     ?tablerow roo:haswhostatus ?whostatus.   #has_WHO_status
        
     ?tablerow roo:P100029 db:triplifier_test_HN_Maastricht.neoplasm.
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100244 ?tstage. 	 #has_T_stage
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100242 ?nstage. 	 #has_N_stage
      
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100241 ?mstage. 	 #has_M_stage
            
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100219 ?ajcc. 		 #has_AJCC_stage
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100202 ?tumour.		 #tumourSite
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P10032 ?metastasis. 	 #has_metastasis
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100022 ?localrecurrence, ?localrecurrencedays, ?regionalrecurrence, ?regionalrecurrencedays, ?metastasisdays.  #has_finding
        
     ?localrecurrencedays roo:P100027 db:triplifier_test_HN_Maastricht.dayClass.
        
     ?regionalrecurrencedays roo:P100027 db:triplifier_test_HN_Maastricht.dayClass.
        
     ?metastasisdays roo:P100027 db:triplifier_test_HN_Maastricht.dayClass.
        
     db:triplifier_test_HN_Maastricht.neoplasm roo:P100403 db:triplifier_test_HN_Maastricht.radiotherapy. #treated_by 
        
     db:triplifier_test_HN_Maastricht.radiotherapy roo:P100027 ?rttotaldays. #has_unit
        
     ?rttotaldays roo:P100027 db:triplifier_test_HN_Maastricht.dayClass.
        
     db:triplifier_test_HN_Maastricht.radiotherapy roo:P100023 ?graytotaldose. #has_dose
        
     ?graytotaldose roo:P100027 db:triplifier_test_HN_Maastricht.Gray.
        
     db:triplifier_test_HN_Maastricht.radiotherapy roo:P100214 ?graydoseperfraction.   #has_dose_per_fraction
     
     ?graydoseperfraction roo:P100027 db:triplifier_test_HN_Maastricht.Gray.
      
     ?tablerow roo:P100403 ?surgery.     #treated_by
     
     ?tablerow roo:P100254 ?survival.    #has_death_finding 
        
     ?tablerow roo:has ?overallsurvivaldays.
        
     ?overallsurvivaldays roo:P100027 db:triplifier_test_HN_Maastricht.dayClass.
        
     ?tablerow roo:C94626 ?chemo.        #chemo_administered
      
        
     db:triplifier_test_HN_Maastricht owl:equivalentClass ncit:C16960.
        
     db:triplifier_test_HN_Maastricht.id owl:equivalentClass ncit:C25364.
        
     db:triplifier_test_HN_Maastricht.biological_sex owl:equivalentClass ncit:C28421.
        
     db:triplifier_test_HN_Maastricht.age_at_diagnosis owl:equivalentClass roo:C100003.
    
     db:triplifier_test_HN_Maastricht.overall_hpv_p16_status owl:equivalentClass ncit:C128839.
        
     db:triplifier_test_HN_Maastricht.pretreat_hb_in_mmolperlitre owl:equivalentClass roo:asaScore.
        
     db:triplifier_test_HN_Maastricht.performance_status_ecog owl:equivalentClass roo:whostatus.
        
     db:triplifier_test_HN_Maastricht.clin_t owl:equivalentClass ncit:C48885.
     
     db:triplifier_test_HN_Maastricht.clin_n owl:equivalentClass ncit:C48884.

	 db:triplifier_test_HN_Maastricht.clin_m owl:equivalentClass ncit:C48883.
        
     db:triplifier_test_HN_Maastricht.ajcc_stage owl:equivalentClass roo:C100052.
        
     db:triplifier_test_HN_Maastricht.cancer_surgery_performed owl:equivalentClass ncit:C17173.
      
     db:triplifier_test_HN_Maastricht.index_tumour_location owl:equivalentClass ncit:C3263.
                
     db:triplifier_test_HN_Maastricht.event_local_recurrence owl:equivalentClass roo:localrecurrence.

	 db:triplifier_test_HN_Maastricht.local_recurrence_in_days owl:equivalentClass roo:localrecurrencedays.

	 db:triplifier_test_HN_Maastricht.event_locoregional_recurrence owl:equivalentClass roo:regionalrecurrence.

	 db:triplifier_test_HN_Maastricht.locoregional_recurrence_in_days owl:equivalentClass roo:regionalrecurrencedays.
        
     db:triplifier_test_HN_Maastricht.event_distant_metastases owl:equivalentClass ncit:C19151.
               
     db:triplifier_test_HN_Maastricht.distant_metastases_in_days owl:equivalentClass roo:metastasisdays.
        
     db:triplifier_test_HN_Maastricht.event_overall_survival owl:equivalentClass ncit:C25717.

	 db:triplifier_test_HN_Maastricht.overall_survival_in_days owl:equivalentClass roo:overallsurvivaldays.
        
     db:triplifier_test_HN_Maastricht.chemotherapy_given owl:equivalentClass ncit:C94626.
    
     db:triplifier_test_HN_Maastricht.yearClass a rdfs:Class.  
   
     db:triplifier_test_HN_Maastricht.yearClass rdfs:label "Years".
      
     db:triplifier_test_HN_Maastricht.dayClass a rdfs:Class.
    
     db:triplifier_test_HN_Maastricht.dayClass rdfs:label "Days".
        
     db:triplifier_test_HN_Maastricht.Gray a rdfs:Class. 
  
     db:triplifier_test_HN_Maastricht.Gray rdfs:label "Gy".
    
     db:triplifier_test_HN_Maastricht.neoplasm a rdfs:Class. 
        
     db:triplifier_test_HN_Maastricht.neoplasm rdfs:label "Neoplasm".
        
     db:triplifier_test_HN_Maastricht.radiotherapy a rdfs:Class. 
    
     db:triplifier_test_HN_Maastricht.radiotherapy rdfs:label "Radiotherapy".
    
     db:triplifier_test_HN_Maastricht.yearClass owl:equivalentClass ncit:C29848.
      
     db:triplifier_test_HN_Maastricht.dayClass owl:equivalentClass ncit:C25301. 
    
     db:triplifier_test_HN_Maastricht.Gray owl:equivalentClass ncit:C18063.
    
     db:triplifier_test_HN_Maastricht.neoplasm owl:equivalentClass ncit:C3262.
    
     db:triplifier_test_HN_Maastricht.radiotherapy owl:equivalentClass ncit:C15313.
    
     dbo:has_value owl:sameAs roo:P100042.    #has_value
       
    } 
  

WHERE

{  
    ?tablerow rdf:type db:triplifier_test_HN_Maastricht.
    
	?tablerow dbo:has_column ?patientID, ?gender, ?age, ?tumour, ?whostatus, ?hpv, ?tstage, ?nstage, ?mstage, ?ajcc, ?asa, ?surgery, ?chemo, ?rttotaldays, ?graytotaldose, ?graydoseperfraction, ?survival, ?overallsurvivaldays, ?localrecurrence, ?localrecurrencedays, ?regionalrecurrence, ?regionalrecurrencedays, ?metastasis, ?metastasisdays.
    
   # ?years rdf:type db:triplifier_test_HN_Maastricht.yearClass. 
        
   # ?days rdf:type db:triplifier_test_HN_Maastricht.dayClass.
    
   # ?gy rdf:type db:triplifier_test_HN_Maastricht.Gray.
             
   # ?neoplasm rdf:type db:triplifier_test_HN_Maastricht.neoplasm. 
    
   # ?radiotherapy rdf:type db:triplifier_test_HN_Maastricht.radiotherapy. 
    
    ?patientID rdf:type db:triplifier_test_HN_Maastricht.id. 
    
    ?gender rdf:type db:triplifier_test_HN_Maastricht.biological_sex.
    
    ?age rdf:type db:triplifier_test_HN_Maastricht.age_at_diagnosis.
    
    ?tumour rdf:type db:triplifier_test_HN_Maastricht.index_tumour_location.
    
    ?whostatus rdf:type db:triplifier_test_HN_Maastricht.performance_status_ecog.
    
    ?hpv rdf:type db:triplifier_test_HN_Maastricht.overall_hpv_p16_status.
    
    ?tstage rdf:type db:triplifier_test_HN_Maastricht.clin_t.
    
    ?nstage rdf:type db:triplifier_test_HN_Maastricht.clin_n.
    
    ?mstage rdf:type db:triplifier_test_HN_Maastricht.clin_m.
    
    ?ajcc rdf:type db:triplifier_test_HN_Maastricht.ajcc_stage.
    
    ?asa rdf:type db:triplifier_test_HN_Maastricht.pretreat_hb_in_mmolperlitre.
    
    ?surgery rdf:type db:triplifier_test_HN_Maastricht.cancer_surgery_performed.
    
    ?chemo rdf:type db:triplifier_test_HN_Maastricht.chemotherapy_given.
    
    ?rttotaldays rdf:type db:triplifier_test_HN_Maastricht.radiotherapy_total_treat_time.
        
    ?graytotaldose rdf:type db:triplifier_test_HN_Maastricht.radiotherapy_refgydose_total_highriskgtv.
        
    ?graydoseperfraction rdf:type db:triplifier_test_HN_Maastricht.radiotherapy_number_fractions_highriskgtv.
        
    ?survival rdf:type db:triplifier_test_HN_Maastricht.event_overall_survival.
        
    ?overallsurvivaldays rdf:type db:triplifier_test_HN_Maastricht.overall_survival_in_days.
        
    ?localrecurrence rdf:type db:triplifier_test_HN_Maastricht.event_local_recurrence.
        
    ?localrecurrencedays rdf:type db:triplifier_test_HN_Maastricht.local_recurrence_in_days.
    
    ?regionalrecurrence rdf:type db:triplifier_test_HN_Maastricht.event_locoregional_recurrence.
    
    ?regionalrecurrencedays rdf:type db:triplifier_test_HN_Maastricht.locoregional_recurrence_in_days.
        
    ?metastasis rdf:type db:triplifier_test_HN_Maastricht.event_distant_metastases.
        
    ?metastasisdays rdf:type db:triplifier_test_HN_Maastricht.distant_metastases_in_days.
    
  
}
